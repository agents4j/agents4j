plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.18.0'
}

version = '0.1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:30.1.1-jre'

    // LangChain4J BOM for consistent version management
    api platform('dev.langchain4j:langchain4j-bom:1.0.0')

    // LangChain4J dependencies - versions managed by BOM
    api 'dev.langchain4j:langchain4j'
    api 'dev.langchain4j:langchain4j-core'

    // Testing dependencies
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
}

testing {
    suites {
        // Configure the built-in test suite
        test {
            // Use JUnit Jupiter test framework
            useJUnitJupiter('5.8.1')
        }
    }
}

jreleaser {
  gitRootSearch = true
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        'release-deploy' {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'cloud.savantly'
            artifactId = 'agents4j'

            from components.java

            pom {
                name = 'agents4j'
                description = 'Java Agent Library'
                url = 'https://github.com/agents4j/agents4j'

                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'jdbranham'
                        name = 'Jeremy Branham'
                    }
                    developer {
                        id = 'charathram'
                        name = 'Charath Ranganathan'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/agents4j/agents4j.git'
                    developerConnection = 'scm:git:ssh://github.com/agents4j/agents4j.git'
                    url = 'https://github.com/agents4j/agents4j'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
