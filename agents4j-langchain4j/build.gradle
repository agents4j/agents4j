plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.18.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    // API dependency - contains all the interfaces and contracts
    api project(':agents4j-api')

    // Core implementation dependency
    implementation project(':agents4j-core')

    // LangChain4J BOM for consistent version management
    api platform('dev.langchain4j:langchain4j-bom:1.0.0')

    // LangChain4J dependencies - versions managed by BOM
    api 'dev.langchain4j:langchain4j'
    api 'dev.langchain4j:langchain4j-core'

    // Optional LangChain4J integrations that users might want
    implementation 'dev.langchain4j:langchain4j-open-ai'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core:5.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
}

testing {
    suites {
        test {
            useJUnitJupiter('5.8.1')
        }
    }
}

jreleaser {
  gitRootSearch = true
  signing {
    active = 'ALWAYS'
    armored = true
  }
  deploy {
    maven {
      mavenCentral {
        'release-deploy' {
          active = 'ALWAYS'
          url = 'https://central.sonatype.com/api/v1/publisher'
          stagingRepository('build/staging-deploy')
        }
      }
    }
  }
  release {
    github {
      enabled = true
      update {
        enabled = true
      }
      skipTag = true
    }
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.agents4j'
            artifactId = 'agents4j-langchain4j'

            from components.java

            pom {
                name = 'agents4j-langchain4j'
                description = 'LangChain4J integration for Agents4J'
                url = 'https://github.com/agents4j/agents4j'

                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'jdbranham'
                        name = 'Jeremy Branham'
                    }
                    developer {
                        id = 'charathram'
                        name = 'Charath Ranganathan'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/agents4j/agents4j.git'
                    developerConnection = 'scm:git:ssh://github.com/agents4j/agents4j.git'
                    url = 'https://github.com/agents4j/agents4j'
                }
            }
        }
    }
    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}
